@echo off
:: Process Playstation Vita shaders

:: Copyright 1995-2014 by Rebecca Ann Heineman becky@burgerbecky.com

:: It is released under an MIT Open Source license. Please see LICENSE
:: for license details. Yes, you can use it in a
:: commercial title without paying anything, just give me a credit.
:: Please? It's not like I'm asking you for money!

SETLOCAL
SETLOCAL ENABLEDELAYEDEXPANSION

:: Using a simple state machine, extract the custom
:: command switches -header, -object, -label and -gxp

SET state=normal
SET VITA_PARMS=
SET VITA_HEADER=
SET VITA_OBJECT=
SET VITA_LABEL=
SET VITA_GXP=

FOR %%a IN (%*) DO (
	SET curarg=%%a
	IF /I "%%a" EQU "-object" (
		SET state=expecting_object
	) ELSE IF /I "%%a" EQU "-header" (
		SET state=expecting_header
	) ELSE IF /I "%%a" EQU "-label" (
		SET state=expecting_label
	) ELSE IF /I "%%a" EQU "-gxp" (
		SET state=expecting_gxp
	) ELSE (
		IF "!state!" EQU "expecting_object" (
			SET VITA_OBJECT=!curarg!
		) ELSE IF "!state!" EQU "expecting_header" (
			SET VITA_HEADER=!curarg!
		) ELSE IF "!state!" EQU "expecting_label" (
			SET VITA_LABEL=!curarg!
		) ELSE IF "!state!" EQU "expecting_gxp" (
			SET VITA_GXP=!curarg!
	        ) ELSE (
			SET VITA_PARMS=!VITA_PARMS! !curarg!
		)
		SET state=normal
	)
)

:: Perform the compilation of the shader and abort on error

echo "%SCE_PSP2_SDK_DIR%\host_tools\bin\psp2cgc.exe" !VITA_PARMS! -o !VITA_GXP! >&2
"%SCE_PSP2_SDK_DIR%\host_tools\bin\psp2cgc.exe" !VITA_PARMS! -o !VITA_GXP!
IF ERRORLEVEL 1 goto :exitnow

:: Using the Burgerlib Dump tool, convert the GXP into
:: C source code if a header is desired

if "%VITA_HEADER%" == "" goto skipheader

:: Test if the file can be written
echo // > %VITA_HEADER% || rem
IF ERRORLEVEL 1 goto :exitnow

echo // Generated by the Vita Header compiler batch file >> %VITA_HEADER%
echo // Copyright 2015, Rebecca Ann Heineman >> %VITA_HEADER%
echo // >> %VITA_HEADER%
echo const unsigned char %VITA_LABEL%image[] __attribute__((__aligned__(4))) = { >> %VITA_HEADER%
dump -c %VITA_GXP% >> %VITA_HEADER%
IF ERRORLEVEL 1 goto :exitnow
echo }; >> %VITA_HEADER%
echo #define %VITA_LABEL% (*((const SceGxmProgram *)%VITA_LABEL%image)) >> %VITA_HEADER%
echo #define %VITA_LABEL%size sizeof(%VITA_LABEL%image) >> %VITA_HEADER%

:: Using the Sony p2p2bin tool, convert the GXP into a directly
:: linkable OBJ file if requested

:skipheader
if "%VITA_OBJECT%" == "" goto exitnow
echo "%SCE_PSP2_SDK_DIR%\host_tools\build\bin\psp2bin.exe" !VITA_GXP! -b2e PSP2,!VITA_LABEL!,!VITA_LABEL!size,4 -o !VITA_OBJECT! >&2
"%SCE_PSP2_SDK_DIR%\host_tools\build\bin\psp2bin.exe" !VITA_GXP! -b2e PSP2,!VITA_LABEL!,!VITA_LABEL!size,4 -o !VITA_OBJECT!

:: Exit with the error code

:exitnow
exit /b %ERRORLEVEL%
